class Account {
  abstract;
  email;
  password;
  name;
  imageURL;
  pronouns;
  Integer id;
}

class Owner {
  isA Account;
}

class Instructor {
  isA Account;
}

class Customer {
  isA Account;
}

class Registration {
  * registrations -> 1 Customer customer;
  * registrations -> 1 Session session;
}

class SessionPackage {
  Integer priceReduction;
  Integer duration;
  Date first;
  0..3 packages -> 1 Course course;
}

class Session {
  Time startTime;
  Time endTime;
  Date date;
  Integer capacity;
  Integer id;
  * sessions -> 1 Instructor supervisor;
  * sessions -> 1 Course courseType;
}

class Location {
  Integer id;
  floor;
  room;
  1 location <- * Session sessions;
}

class Course {
  name;
  Difficulty difficulty;
  Status status;
  description;
  Integer id;
  Integer pricePerHour;
  category;
  url;
  enum Difficulty {Beginner, Intermediate, Advanced}
  enum Status {Approved, Pending, Closed, Disapproved}
}

class BillingAccount {
  cardNumber;
  cardHolder;
  billingAddress;
  Integer cvv;
  Date expirationDate;
  boolean isDefault;
  Integer id;
  0..* billingAccounts -> 1 Customer customer;
}


class SportCenter {
  Integer id;
  name;
  Time openingTime;
  Time closingTime;
  address;
  email;
  phoneNumber;

  1 center <@>- * Course courses;
  1 center <@>- * Location locations;
  1 center <@>- * Account accounts;
}

